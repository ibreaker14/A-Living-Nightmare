"A Living Nightmare" by Mingtau Li

The story genre is "Adventure". 

The release number is 1. 

The story headline is "A living nightmare". 

The story description is "You woke up on the fourth floor at the  ECS building at CSULB in the middle of the night. It is hard to see. All the lights seem to be turned off. Still disoriented from having just woken up, you stumble into the Linux room.
	
The last thing you remembered was you going to bed after a night of gambling and drinking  with some friends at the casino.
	
You breathe in and out slowly in the attempt to collect your thoughts. You examine your surroundings. It is dark and cold. You are freezing and it is hard to see. [line break]"
	
The story creation year is 2017.

[Tells program what to print when game loads]
After printing the banner text:
	say "[line break][line break][story description]".
	


Section 0 - Terms

[Limit inventory]
The carrying capacity of the player is 5.

[Defines "use" as a new action]
Understand the command "use" as something new. Understand "use [something]" as using. 
Using is an action applying to one thing. 
Carry out using:
	say "Using [noun].".
Understand "use [device]" as switching on.

[Defines alias for using]
Understand "play with [something]" as using.

[Defines "flush" as a new action]	
Understand the command "flush" as something new. Understand "flush [something]" as flushing. 
Flushing is an action applying to one thing.
Carry out flushing:
	say "You can't do that here[line break]".
	
[Defines "hack" as a new action]
Understand the command "hack" as something new. Hacking is an action applying to one thing. Understand "hack [something]" as hacking.
Carry out hacking:
	say "You can't do that here[line break]";

[Understand "prying" as a new action player can execute on something using another object]
Understand "pry [something] with [something preferably held]" as prying it with.
Prying it with is an action applying to one thing and one carried thing.
Check prying it with:
	say "I don't think you're supposed to do that here[line break]" .

[Defines aliases for attacking]
Understand "kick [something]" as attacking.
Understand "smash [something]" as attacking.
Understand "headbutt [something]" as attacking.

[Defines aliases for attacking an object]
Understand "attack [something] with [something preferably held]" as attacking it with.
Attacking it with is an action applying to one thing and one carried thing.
Understand "smash [something] with [something preferably held]" as attacking it with.
Understand "break [something] with [something preferably held]" as attacking it with.

[Defines default action for attacking]
Check attacking it with:
	say "Wow that is a sturdy [the noun][line break]" .
	
[Defines and initializes boolean values]
Hacked is a truth state that varies. Hacked is false.
Hackable is a truth state that varies. Hackable is false.

[Defines sentinel for reading]
A thing can be readable.  A thing is usually not readable. 

Section 1 - The Linux Room

[Adds a new room with objects]
The linux room is a room. "All the computers in the room seem to be turned off except for one - the master computer."
The master computer is a container in the linux room. 
The carrying capacity of the master computer is one.

[Hide status of what is in computer without examination]
Rule for printing the name of master computer:
	say "[printed name of master computer]";
	omit contents in listing.
	
[Defines master computer]
The indefinite article of the master  computer is "the".
The description of the master computer is "[if flashdrive is in the computer]The computer can now be hacked.[else]There is nothing worthy of note here. " .

[Disables putting computer into inventory]
Instead of taking the master computer:
	say "I wouldn't do that if I were you.[line break]".

[Sentinel fail case]
Instead of inserting something not readable into master computer: 
	say "You can't put that in there";
	stop the action.

[Changes status of hackable]
After inserting flashdrive into master computer:
	say "You have inserted the hack drive.";
	say "[description of the master computer]";
	now hackable is true;

[Defines what to do once computer is hackable]
Instead of hacking the master computer:
	if hackable is true:
		if hacked is false:
			now the command prompt is "Solve the equation to complete hack: ((36*(2704/16)^(1/2))*(117/3))/3) >";
			continue the action;
		otherwise:
			say "Computer has already been hacked.";
	otherwise: 
		say "It doesn't seem like you can hack it.".

[Scanner for user input. Changes hacked status to true if computer hack is successful]
After reading a command when the command prompt is  "Solve the equation to complete hack: ((36*(2704/16)^(1/2))*(117/3))/3) >":
	if the player's command matches "6084":
		now hacked is true;
		say "computer successfully hacked. You have uncovered a hidden file with the message with the message: [quotation mark]1124[quotation mark].";
		now the command prompt is ">";
		say line break;
		say run paragraph on;
		reject the player's command;
	otherwise:
		say "> hack failed <[line break]";
		now the command prompt is ">";
		say line break;
		say run paragraph on;
		reject the player's command.

[Redefines computer description after hack]
Instead of examining computer when hacked is true:
	say "You have uncovered a hidden file with the message with the message: [quotation mark]1124[quotation mark].".

[Defines default action of clock]
Instead of using digital clock:
	if batteries are in the clock:
		say "The time is 2:15AM";
	otherwise:
		say "I don't think we can use this anymore. As a paperweight, maybe".

[Defines clock and default action]
A digital clock is an openable container in the Linux room.  
digital clock is closed.
The description of the clock is "A small digital alarm clock[if  batteries are in the digital clock] next to the master computer. The time is 2:15AM [else].".
Understand "clock" as digital clock. 
batteries are inside the digital clock. 
The indefinite article of batteries is "some".
Instead of using batteries:
	say "You have to put the batteries into something in order to use it[line break]";

[Disables printout of clock status]
Rule for printing the name of clock:
	say "[printed name of clock]";
		omit contents in listing.

Section 2 - The Corridor

[Defines new room]
The corridor is a room. 
The corridor is north of the linux room.
The description of the corridor is "[if player is not carrying flashlight]You kicked something that's on the floor. It seems to be a small object[otherwise if flashlight is switched on] The corridor is a long hallway that connects rooms together[end if].".

[Defines flashlight and default action of flashlight]
A flashlight is a device in the corridor. 
It is privately-named. 
The printed name is "[if player is not carrying flashlight]small object[otherwise]flashlight[end if]".
The description of flashlight is "It seems to be a small flashlight". 
Flashlight is not marked for listing. 

[Defines aliases for flashlight]
Understand "small object" as flashlight.
Understand "flashlight" as flashlight.

[Stops player from going into anywhere else but Linux room if flashlight is not switched on]
Instead of going north in the corridor when flashlight is switched off:
	say  "You can't go that way. You can't see!".
	
Instead of going northeast in the corridor when flashlight is switched off:
	say  "You can't go that way. You can't see!".
	
Instead of going northwest in the corridor when flashlight is switched off:
	say  "You can't go that way. You can't see!".
	
Instead of going east in the corridor when flashlight is switched off:
	say  "You can't go that way. You can't see!".
	
Instead of going west in the corridor when flashlight is switched off:
	say  "You can't go that way. You can't see!".
	
Instead of going southeast in the corridor when flashlight is switched off:
	say  "You can't go that way. You can't see!".
	
Instead of going southwest in the corridor when flashlight is switched off:
	say  "You can't go that way. You can't see!".
	
[Defines flashlight parameters]
Before switching on flashlight:
	if player is not carrying flashlight: 
		say "you can't use something you don't have.";
		stop the action;
	if player is not carrying batteries:
		say "it seems the flashlight is missing some batteries.";
		stop the action.

[Defines action for dropping flashlights]
Before dropping batteries:
	if player is carrying flashlight:
		say "Dang it! Dropped the batteries. Now the flashlight won't work.";
		now flashlight is switched off.

[Defines default action for non-working flashlight]
Rule for listing nondescript items of the corridor:
	if flashlight is not switched on: 
		say "It's too dark. You can't see anything.";
		stop the action.
		
[Disables insertion of foreign objects into clock]
Instead of inserting something that is not batteries into clock:
	say "Well that doesn't belong there.".
	
[Turns flashlight off when batteries are not inserted]
After inserting batteries into something:
	now flashlight is switched off.
	
[Default output for battery insertion]
Instead of inserting batteries into flashlight:
	say "batteries have already been inserted".
	
[Defines output for dropping batteries in the Linux room]
Instead of dropping batteries in the Linux room:
	say "I don't feel like dropping precious batteries here".
	
[Defines output for dropping flashlight in the Linux room]
Instead of dropping flashlight in the Linux room:
	say "I dont' feel like dropping the flashlight in here."

[Defines default action for non-working flashlight]
Before of going to the corridor when flashlight is not switched on or player is not carrying batteries:
	if player is in Linux room:
		continue the action;
	otherwise: 
		if flashlight is switched off:
			say "it's so dark";
		if player is not carrying batteries and player is not carrying flashlight:
			say "I seem to have misplaced something.";
		stop the action.

[Defines action for dropping flashlight]
Instead of dropping flashlight:
	now flashlight is switched off;
	continue the action.

[Defines action for switching on flashlight for the first time in the corridor]
After switching on flashlight in the corridor for the first time:
	say "[description of the corridor][line break]";
	say "But I don't see a staircase though. That's weird. This isn't the school I know. What's happening?".
	
Section 3 - Anthony Giacalone's Room

[Defines new room and its objects]
Anthony Giacalone's office is northwest of the corridor.

The drawer is an openable container. 
The drawer is in Anthony Giacalone's office. 
The drawer is closed.

A rope is in the drawer.

A crowbar is in the safe. Crowbar unlocks elevator.

[Defines cd]
A Justin Beiber cd is in the drawer. 
The description of the Justin Beiber is "Justin Beiber's Never Say Never album.".
Understand "cd" as Justin Beiber cd.

[Defines flashdrive]
A flashdrive is in the drawer.
flashdrive is readable.
Understand "thumb drive","thumbdrive","memory stick","drive", and "usb" as flashdrive.

[Defines pen]
A pen is in the drawer. 
The description of pen is "This pen is Royal Blue.".

[Defines safe]
The safe is a locked openable container in Anthony Giacalone's office. 
The description of the safe is "A safe that is opened using a passcode".

[Aliases for examining safe]
Understand "use [safe]" as examining.
Understand "unlock [safe]" as examining. 

[Defines use for cd]
Instead of using Justin Beiber cd:
	say "You use the cd to scratch an itch.".
	
[Defines use for rope]
Instead of using rope:
	say "You play jump rope with yourself.".
	
[Defines action upon examination of safe]
After examining the safe:
	if safe is locked:
		now the command prompt is "Please enter the required password. >";
		continue the action.

[Scanner for user input]
After reading a command when the command prompt is "Please enter the required password. >":
	if the player's command matches "1124":
		now the safe is unlocked;
		say "safe is now unlocked.";
		now the command prompt is ">";
		say line break;
		say run paragraph on;
		reject the player's command;
	otherwise:
		say "> invalid password <[line break]";
		now the command prompt is ">";
		say line break;
		say run paragraph on;
		reject the player's command.

[Disables listing and status of safe]
Rule for printing the name of safe:
	say "[printed name of safe]";
		omit contents in listing.

[Defines description of locked safe]
Before opening safe:
	if safe is locked:
		say "You need the password to open this safe.".

[Defines printout of safe after it's open]		
After opening safe:
	say "There is nothing in the safe.... Just kidding. There is crowbar in the safe".

[Defines use of flashdrive]
Instead of using flashdrive:
	if player is in the linux room:
		say "Try putting the flashdrive into the computer.";
	otherwise:
		say "You can't use that here.".

Section 4 - Windows Room

The windows room is west of the corridor.
[Nothing to do here. Have fun wasting your time here].

Section 5 - Lecture Room

[Defines new room and its objects]
The lecture room is southwest of the corridor. 

A trashcan is a container in the lecture room.  
Rule for printing the name of trashcan:
	say "[printed name of trashcan]";
	omit contents in listing.
	
[Defines use of trashcan]
Instead of using trashcan:
	say "You have nothing else to throw away, except maybe your hopes and dreams.".
	
[Defines frisbee and use of frisbee]
A frisbee is in the lecture room. 	
Instead of using frisbee:
	say "Wheeee![line break]".
	
[Defines banana and use of banana]
A banana  is in the lecture room. 
The description of banana peel is "A soiled banana peel probably left over by a student.".
Instead of using banana peel:
	say "You slipped on the peel, you klutz.".
	
[Alias for banana peel]
Understand "peel" as banana peel.
	
[Defines whiteboard]
A whiteboard is an object that is fixed in place in the lecture room.
The description of whiteboard is "There seems to be a message on the whiteboard. It says: [quotation mark]Edward.....[quotation mark]. ".

[Alias for whiteboard]
Understand "board" as whiteboard.

[Defines stapler]
A stapler is in the lecture room. 
The description of the stapler is "Just an ordinary stapler.".

Section 6 - Bathroom

[Defines new room and objects in the room]
The bathroom is southeast of the corridor.

[There are multiple toilets in the room. Player can refer to a toilet as a single object]
toilets is an enterable container that is fixed in place in the bathroom.
The indefinite article of toilets is "some".
Understand "toilet" as toilets.

[There are multiple sinks in the room. Player can refer to a sink as a single object]
sinks are objects that is fixed in place in the bathroom.
The indefinite article of sinks is "some".
Understand "sink" as sinks.

[There are multiple urinals in the room. Player can refer to a urinal as a single object]
urinals are objects that is fixed in place in the bathroom.
The indefinite article of urinals is "some".
Understand "urinal" as urinals.

[Defines use for urinals]
Instead of using urinals:
	say "Wheeee!!!![line break]".

[Defines use for sinks]
Instead of using sinks:
	say "There doesn't seem to be any water.";

[Disables status of toilet from listing]
Rule for printing the name of toilets:
	say "[printed name of toilets]";
		omit contents in listing.

[Defines action for entering toilet]
Instead of entering toilets:
	say "You sit on a toilet.";
	move the player to toilets, without printing a room description.
	
[Defines action for entering toilet]
Instead of going in toilets:
	say "You sit on a toilet.";
	move the player to toilets, without printing a room description.
	
[Flushes toilet]
Instead of switching on toilets: 
	say "You flushed the toilet.".
	
[Defines default behavior for using toilets in the bathroom]
Instead of using toilets:
	if player is not in toilets:
		say "Hold on, buddy! At least get into a toilet first.";
		stop the action;
	otherwise:
		say "BRRUUIPPFFFFFTTTT![line break]".

[Defines action for opening toilet]
Instead of opening toilets: 
	say "Someone left a huge masterpiece in here.";
	continue the action.
	
[Defines printout for closing toilet]
Instead of closing toilets:
	say "You don't wanna touch that.".
	
[Defines printout for flushing toilet]
Instead of flushing toilets:
	say "Dang it! There's no water.".
	
[Defines printout for examining toilet]
Instead of examining toilets:
	say "It's just a toilet.".

[Defines printout for smelling bathroom]
Instead of smelling bathroom:
	say "You gag from the horrendous smell.".
	
[Defines printout for smelling toilet]
Instead of smelling toilets:
	say "There must be something seriously wrong with you.".
	
[Aliases for pooping]
Understand "take a shit in [toilets]", "shit [toilets]","poop in [toilets]","take a dump in [toilets]", and "drop a deuce in [toilets]" as using.

[Alias for peeing]
Understand "pee in [urinals]" and "take a piss [urinals]" as using.

[Aliases for exiting]
Understand "get out", "get off", and "out" as exiting.

Section 7 - Elevator

[Defines elevator and shaft]
The elevator is a locked door. 
The elevator is east of the corridor. 

The shaft is a room. 
The description of the shaft is "knees weak, arms are heavy.".
The shaft is east of the elevator.

[Defines use for elevator]
Instead of using elevator:
	say "There doesn't seem to be any power.".

[Defines printout for examining elevator]
Instead of examining the elevator:
	say "You see an [other side of the elevator] with a sheer drop of I-don't-know-how-many-feet-but-it-looks-super-high.".
	
[Defines printout for searching elevator]
Instead of searching the elevator:
	say "You see an [other side of the elevator] with a sheer drop of I-don't-know-how-many-feet-but-it-looks-super-high.".

[Defines action for unlocking elevator with crowbar]
After unlocking elevator with crowbar:
	now elevator is open;
	say "[description of shaft]";
	say "You see an [other side of the elevator] with a sheer drop of I-don't-know-how-many-feet-but-it-looks-super-high.".
	
[Defines action for entering shaft]
Instead of going to shaft when elevator is open:
	say "Woah! You almost ran into the elevator shaft. Are you sure? >";
	if player consents:
		continue the action;
	otherwise:
		say "Whew! that was close.";
		stop the action.
	
[Defines action for prying elevator open]
Instead of prying the locked elevator with crowbar:
	say "You pried the elevator door open.";
	say "[description of shaft]";
	say "You see an [other side of the elevator] with a sheer drop of I-don't-know-how-many-feet-but-it-looks-super-high.";
	now elevator is unlocked;
	now elevator is open.
	
Section 8 - Database Room

[Defines new room an its contents]
The database room is northeast of the corridor.
A book is in the database room. 
The description of book is "There seems to be a page missing.".

[Defines use for book]
Instead of using book:
	say "[description of book]";

Section 9 - The Window

[Defines window and schoolyard outside of window]
The window is a locked door. It is north of the corridor. 
The schoolyard is north of the window. The description of the window is "A glass window.".

[Alias for window]
Understand "glass" as window.

[Defines use for window]
Instead of using the window:
	say "You see a [the other side of the window] on the other side.";

[Defines printout for searching widow]
Instead of searching the window:
	say "There seems to be [the other side of the window] on the other side.".
	
[Defines printout for opening window]
Instead of opening the window when window is locked:
	say "Window can't be opened. This is one tough cookie.".
	
[Defines printout for going through closed window]
Instead of going through the closed window: 
	say "The window is shut.".
	
[Defines printout for attacking window]
Instead of attacking closed window:
	say "You cracked the window! What is wrong with you?[line break]".
	
[Defines action for attacking window a second time]
Instead of attacking closed window for the second time:
	say "Uh-oh you broke the window. Be careful! There's at least a 200ft drop onto the [the other side of the window] outside.";
	now window is unlocked;
	now window is open.

[Defines action for attacking window with crowbar]
Instead of attacking closed window with crowbar:
	now window is unlocked;
	now window is open;
	say "You broke the window.";
	say " Be careful! There's at least a 200ft drop onto the [the other side of the window] outside.".
	
[Defines action for prying window open]
Instead of prying closed window with crowbar:
	now window is unlocked;
	now window is open;
	say "The whole window frame breaks away from the wall and falls onto the [the other side of the window] outside. Watch your step! There's at least a 200ft drop.".
	
[Defines action for going to schoolyard]
Instead of going to schoolyard when window is open:
	say "Woah! You almost fell out of the window. Are you sure? >";
	if player consents:
		continue the action;
	otherwise:
		say "Whew! that was close";
		stop the action.
		
Section 10 - End Game

[Defines 2 possible routes for beating game]

[Route 1 of 2]
After going to the schoolyard:
	end the story saying "[line break]You fell 255 feet to your death, your scattered body parts splatter all over the schoolyard and your thick, dark blood  is painted onto the walls of the building.[line break][line break]You then wake up and realize you were dreaming all along. [line break]CONGRATULATIONS! YOU WON!  [line break]".
	
[Route 2 of 2]
After going to the shaft:
	end the story saying "[line break]You fell  to your death, There is a loud, echoing splat as your body hits the bottom of the elevator shaft. There is a sickening crunch as your bones break into a thousand pieces that penetrate your skin. You become an urban legend as the ghost who lives in the elevator. [line break][line break]You then wake up and realize you were dreaming all along. [line break]CONGRATULATIONS! YOU WON! [line break]".